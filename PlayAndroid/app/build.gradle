plugins{
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    defaultConfig {
        applicationId "com.dragon.playandroid"
        versionCode 1
        versionName "0.0.1"

        signingConfigs {
            //signingConfigs打包正式版本时才需要放开
            wanandroid {
                keyAlias 'wanandroid.keystore'
                keyPassword 'wanandroid'
                storeFile file('wanandroid.keystore')
                storePassword 'wanandroid'
            }
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true",
                        "room.expandProjection":"true",
//                        AROUTER_MODULE_NAME: project.getName()
                ]
            }
        }
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()

        // Enable Coroutines and Flow APIs
        freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
        freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.FlowPreview"
    }

    //启用dataBinding Compose等
    buildFeatures {
        //noinspection DataBindingWithoutKapt
        dataBinding true
    }

    //构建类型配置
    buildTypes {
        release {
            //signingConfig打包正式版本时才需要放开
            signingConfig signingConfigs.wanandroid
            minifyEnabled true      // 开启代码混淆
            zipAlignEnabled true    // 开启Zip压缩优化
            shrinkResources true    // 移除未被使用的资源
            //release 只arm架构
            ndk {
                abiFilters 'armeabi-v7a','arm64-v8a'
            }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField "String", "VERSION_NAME", '\"' + project.property("versionName").toString() + '\"'
        }

        debug {//开发自己的debug包
            initWith release
            debuggable true
            minifyEnabled false
            shrinkResources false // 移除无用的resource文件
            proguardFiles.clear() // 不混淆
            signingConfig signingConfigs.wanandroid

            ndk {
                abiFilters 'armeabi-v7a','x86'
            }
        }
    }

    //库依赖分析  https://developer.android.google.cn/studio/releases/gradle-plugin?hl=zh-cn#kts
    lintOptions {
        checkDependencies true
    }

}

dependencies {

    implementation project(path: ':module_common:common-base')
    implementation project(path:':module_ui:ft-main')
    implementation project(':module_service:service-di-app')

    implementation(libs.hilt.android)
    kapt(libs.hilt.compiler)
    //内存泄露检测
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.8.1'
}